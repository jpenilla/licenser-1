plugins {
    id 'java-gradle-plugin'
    id 'groovy'

    id 'com.gradle.plugin-publish' version '0.14.0'

    id 'org.cadixdev.licenser' version '0.6.1'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'net.kyori'
version = '0.6.2-SNAPSHOT'
description = 'A simple license header manager for Gradle'

repositories {
    gradlePluginPortal()
}

sourceSets {
    functionalTest {
    }
}

configurations {
    functionalTestImplementation.extendsFrom(testImplementation)
    functionalTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}
gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.register('functionalTest', Test) {
    description = 'Runs the functional tests.'
    group = 'verification'

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath

    mustRunAfter test
}

dependencies {
    implementation gradleApi()

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    functionalTestImplementation gradleTestKit()
}

test {
    useJUnit()
}

tasks.named('check') {
    dependsOn(functionalTest)
}

license {
    include '**/*.java'
    include '**/*.groovy'
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

gradlePlugin {
    plugins {
        licenserPlugin {
            id = 'net.kyori.licenser'
            implementationClass = 'net.kyori.licenser.Licenser'
        }
    }
}

pluginBundle {
    website = 'https://github.com/KyoriPowered/licenser'
    vcsUrl = 'https://github.com/KyoriPowered/licenser'
    description = project.description
    tags = ['license']

    plugins {
        licenserPlugin {
            id = 'net.kyori.licenser'
            displayName = 'licenser'
        }
    }
}
